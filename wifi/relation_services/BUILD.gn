# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//foundation/communication/wifi/wifi/wifi_lite.gni")
} else {
  import("//build/ohos.gni")
  import("//foundation/communication/wifi/wifi/wifi.gni")
}

if (wifi_feature_with_hdi_wpa_supported) {
  local_base_sources = [ "dhcp_service/main.c" ]
} else {
  local_base_sources = [
    "common/wifi_hal_common_func.c",
    "wifi_hal_service/main.c",
    "wifi_hal_service/wifi_hal_adapter.c",
    "wifi_hal_service/wifi_hal_ap_interface.c",
    "wifi_hal_service/wifi_hal_base_interface.c",
    "wifi_hal_service/wifi_hal_callback.c",
    "wifi_hal_service/wifi_hal_chba_interface.c",
    "wifi_hal_service/wifi_hal_chip_interface.c",
    "wifi_hal_service/wifi_hal_crpc_ap.c",
    "wifi_hal_service/wifi_hal_crpc_base.c",
    "wifi_hal_service/wifi_hal_crpc_chip.c",
    "wifi_hal_service/wifi_hal_crpc_common.c",
    "wifi_hal_service/wifi_hal_crpc_p2p.c",
    "wifi_hal_service/wifi_hal_crpc_server.c",
    "wifi_hal_service/wifi_hal_crpc_sta.c",
    "wifi_hal_service/wifi_hal_crpc_supplicant.c",
    "wifi_hal_service/wifi_hal_module/hostapd_hal/wifi_hostapd_hal.c",
    "wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wifi_wpa_common.c",
    "wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wifi_wpa_hal.c",
    "wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wpa_p2p_hal/wifi_p2p_hal.c",
    "wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal/wifi_common_hal.c",
    "wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal/wifi_supplicant_hal.c",
    "wifi_hal_service/wifi_hal_module_manage.c",
    "wifi_hal_service/wifi_hal_p2p_interface.c",
    "wifi_hal_service/wifi_hal_sta_interface.c",
    "wifi_hal_service/wifi_hal_vendor_interface.c",
  ]
}

if (wifi_feature_with_hdi_wpa_supported) {
  local_base_include_dirs = []
} else {
  local_base_include_dirs = [
    "$WIFI_ROOT_DIR/base/state_machine/include",
    "$WIFI_ROOT_DIR/base/cRPC/include",
    "$WIFI_ROOT_DIR/interfaces/inner_api",
    "$WIFI_ROOT_DIR/frameworks/native/interfaces",
    "$WIFI_ROOT_DIR/relation_services/common",
    "$WIFI_ROOT_DIR/relation_services/wifi_hal_service",
    "$WIFI_ROOT_DIR/relation_services/wifi_hal_service/wifi_hal_module/hostapd_hal",
    "$WIFI_ROOT_DIR/relation_services/wifi_hal_service/wifi_hal_module/wpa_supplicant_hal",
    "$WIFI_ROOT_DIR/relation_services/wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal",
    "$WIFI_ROOT_DIR/relation_services/wifi_hal_service/wifi_hal_module/wpa_supplicant_hal/wpa_p2p_hal",
    "$WIFI_ROOT_DIR/services/wifi_standard/include",
  ]
}

if (defined(ohos_lite)) {
  executable("wifi_hal_service") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs +=
        [ "//third_party/wpa_supplicant/wpa_supplicant-2.9/src/common" ]

    deps = [
      "$WIFI_ROOT_DIR/base/cRPC:crpc_server",
      "$WIFI_ROOT_DIR/relation_services/etc/init:etc",
      "//third_party/wpa_supplicant/wpa_supplicant-2.9:wpa_supplicant",
    ]

    external_deps = [
      "bounds_checking_function:libsec_shared",
      "hilog_lite:hilog_shared",
    ]
    cflags = memory_optimization_cflags
    cflags_cc = memory_optimization_cflags_cc
    ldflags = memory_optimization_ldflags
    cflags_cc += [ "-fno-rtti" ]
    defines = [
      "_GNU_SOURCE",
      "OHOS_ARCH_LITE",
      "AP_INTF=\"$wifi_feature_with_ap_intf\"",
      "AP_NUM=$wifi_feature_with_ap_num",
    ]
    ldflags += [ "-lwpa_client" ]
  }
} else {
  ohos_executable("wifi_hal_service") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = true  # Enable/disable control flow integrity detection
      boundary_sanitize = false  # Enable boundary san detection
      cfi_cross_dso = true  # Cross-SO CFI Checks
      integer_overflow = false  # Enable integer overflow detection
      ubsan = false  # Enable some Ubsan options
      cfi_vcall_icall_only = true
      debug = false
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs

    deps = [ "$WIFI_ROOT_DIR/relation_services/etc/init:etc" ]

    if (wifi_feature_with_hdi_wpa_supported) {
    } else {
      deps += [ "$WIFI_ROOT_DIR/base/cRPC:crpc_server" ]

      external_deps = [
        "c_utils:utils",
        "hilog:libhilog",
      ]

      public_external_deps = [ "wpa_supplicant:wpa_client" ]
    }

    defines = [
      "AP_INTF=\"$wifi_feature_with_ap_intf\"",
      "AP_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_local_random_mac) {
      defines += [ "SUPPORT_LOCAL_RANDOM_MAC" ]
    }

    if ("${CONFIG_CTRL_IFACE}" == "unix") {
      defines += [ "WPA_CTRL_IFACE_UNIX" ]
    }

    if (wifi_feature_non_seperate_p2p) {
      defines += [ "NON_SEPERATE_P2P" ]
    }

    cflags_cc = [ "-fno-rtti" ]
    cflags = [ "-Wno-deprecated-non-prototype" ]
    install_images = [ "system" ]
    part_name = "wifi"
    subsystem_name = "communication"
  }
}

group("relation_services") {
  deps = [ ":wifi_hal_service" ]
}
