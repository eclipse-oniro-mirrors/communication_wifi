# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/communication/wifi/wifi/wifi.gni")

ohos_shared_library("wifi_ap_service") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    boundary_sanitize = true
    cfi_cross_dso = true
    debug = false
  }
  install_enable = true
  sources = [
    "ap_config_use.cpp",
    "ap_idle_state.cpp",
    "ap_interface.cpp",
    "ap_monitor.cpp",
    "ap_root_state.cpp",
    "ap_service.cpp",
    "ap_started_state.cpp",
    "ap_state_machine.cpp",
    "ap_stations_manager.cpp",
    "wifi_ap_nat_manager.cpp",
  ]

  include_dirs = [
    "$WIFI_ROOT_DIR/base/state_machine/include",
    "$WIFI_ROOT_DIR/base/cRPC/include",
    "$WIFI_ROOT_DIR/interfaces/inner_api",
    "$WIFI_ROOT_DIR/frameworks/native/interfaces",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/include",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/net_helper",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/config",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/utils",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_ap",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_native/common",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_native/wifi_hal_interface",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_native/client/idl_client",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_native/client/idl_client/idl_interface",
    "$WIFI_ROOT_DIR/utils/inc",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_common",
    "$WIFI_ROOT_DIR/services/wifi_standard/include",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_common/wifi_country_code",
  ]

  cflags_cc = [
    "-std=c++17",
    "-Wall",
  ]

  ldflags = [
    "-fPIC",
    "-Wl,-E",
  ]

  external_deps = [
    "c_utils:utils",
    "dhcp:dhcp_sdk",
    "hilog:libhilog",
    "libxml2:libxml2",
    "netmanager_base:net_conn_manager_if",
  ]

  deps = [
    "$WIFI_ROOT_DIR/base:wifi_base_without_crpc",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_common:wifi_common_service",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_native:wifi_native",
    "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit",
    "$WIFI_ROOT_DIR/utils:wifi_utils",
  ]

  defines = [
    "AP_INTF=\"$wifi_feature_with_ap_intf\"",
    "FEATURE_AP_SUPPORT",
  ]

  if (wifi_feature_with_dhcp_disable) {
    defines += [ "WIFI_DHCP_DISABLED" ]
  }

  if (wifi_feature_with_random_mac_addr) {
    defines += [ "SUPPORT_RANDOM_MAC_ADDR" ]
  }

  if (wifi_feature_with_local_random_mac) {
    defines += [ "SUPPORT_LOCAL_RANDOM_MAC" ]
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.powermgr_battery_manager)) {
    external_deps += [ "battery_manager:batterysrv_client" ]
    defines += [ "HAS_BATTERY_MANAGER_PART" ]
  }

  part_name = "wifi"
  subsystem_name = "communication"
  version_script = "libwifi_ap_service.map"
}
